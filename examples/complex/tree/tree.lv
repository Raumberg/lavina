// tree — recursive directory listing utility
// Usage: tree [path]

import std::os
import std::fs

int dir_count = 0
int file_count = 0

void fn print_tree(string path, string prefix = ""):
    auto entries = fs::list_dir(path)

    int i = 0
    int total = entries.len()
    for ref name in entries:
        // skip hidden files
        if name.substring(0, 1) == ".":
            i += 1
            continue

        bool is_last = (i == total - 1)
        string connector = "├── "
        string child_prefix = "│   "
        if is_last:
            connector = "└── "
            child_prefix = "    "

        string full_path = "${path}/${name}"
        if fs::is_dir(full_path):
            print("${prefix}${connector}${name}/")
            dir_count += 1
            print_tree(full_path, prefix = "${prefix}${child_prefix}")
        else:
            print("${prefix}${connector}${name}")
            file_count += 1

        i += 1

void fn main():
    string root = "."
    auto args = os::args()
    if args.len() > 1:
        root = args[1]

    print(root)
    print_tree(root)
    print("")
    print("${dir_count as string} directories, ${file_count as string} files")
