// dep.lv â€” dependency model and package file parser

string PKG_FILE = "lavina.pkg"

struct Dep:
    string name
    string url
    string version
    string path

// Parse lavina.pkg and return a list of dependencies
// Format: dep <name> <url> <version> [path]
vector[Dep] fn parse_pkg_file():
    vector[Dep] deps = []
    if not fs_exists(PKG_FILE):
        error("No ${PKG_FILE} found in current directory")
        exit(1)

    auto lines = fs_read_lines(PKG_FILE)
    for ref line in lines:
        auto trimmed = line.trim()
        if trimmed == "" or starts_with(trimmed, "#"):
            continue

        if starts_with(trimmed, "dep "):
            auto parts = trimmed.split(" ")
            if parts.len() < 4:
                warn("Invalid dep line: ${trimmed}")
                continue

            string name = parts[1]
            string url = parts[2]
            string version = parts[3]
            string path = ""
            if parts.len() >= 5:
                path = parts[4]

            deps.push(Dep(name, url, version, path))
        else:
            warn("Unknown directive: ${trimmed}")

    return deps
